localrules: all, createAnalysis

rule all:
	input:
		expand("{analysisPath}/logs/create_analysis.log", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/warpedImages/fov_{fov}_zpos_{zpos}.tif", 
			analysisPath = config["global"]["analysisPath"], 
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),

		expand("{analysisPath}/processedImages/fov_{fov}_zpos_{zpos}.npz", 
			analysisPath = config["global"]["analysisPath"], 
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),

		expand("{analysisPath}/decodedImages/fov_{fov}_zpos_{zpos}.npz", 
			analysisPath = config["global"]["analysisPath"], 
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),
		
		expand("{analysisPath}/pixel_score_machine.pkl",
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/extractedBarcodes/fov_{fov}_zpos_{zpos}.h5", 
			analysisPath = config["global"]["analysisPath"], 
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),

		expand("{analysisPath}/exportedBarcodes/barcodes.h5", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/filteredBarcodes/barcodes.h5", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/segmentedImages/fov_{fov}_zpos_{zpos}_DAPI.npz", 
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),

		expand("{analysisPath}/segmentedImages/fov_{fov}_zpos_{zpos}_polyT.npz", 
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),

		expand("{analysisPath}/extractedFeatures/fov_{fov}_zpos_{zpos}_DAPI.shp", 
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),

		expand("{analysisPath}/extractedFeatures/fov_{fov}_zpos_{zpos}_polyT.shp", 
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"]),
		
		expand("{analysisPath}/exportedFeatures/DAPI.shp", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/exportedFeatures/polyT.shp", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/filteredFeatures/DAPI.shp", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/filteredFeatures/polyT.shp", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/assignedBarcodes/barcodes_DAPI.h5", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/assignedBarcodes/barcodes_polyT.h5", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/filteredBarcodes/barcodes_DAPI.h5", 
			analysisPath = config["global"]["analysisPath"]),

		expand("{analysisPath}/filteredBarcodes/barcodes_polyT.h5", 
			analysisPath = config["global"]["analysisPath"])

rule createAnalysis:
	params:
		dataHome = config["global"]["dataHome"],
		md = config["global"]["merfishdecoder"],
		analysisHome = config["global"]["analysisHome"],
		dataSetName = config["global"]["dataSetName"],
		codebookName = config["global"]["codebookName"],
		dataOrganizationName = config["global"]["dataOrganizationName"],
		microscopeParameterName = config["global"]["microscopeParameterName"],
		microscopeChromaticAberrationName = config["global"]["microscopeChromaticAberrationName"],
		positionName = config["global"]["positionName"]
	
	output:
		"{analysisPath}/logs/create_analysis.log"
	
	shell:
		"""
		python {params.md} create-analysis \
			--data-set-name={params.dataSetName} \
			--codebook-name={params.codebookName} \
			--data-organization-name={params.dataOrganizationName} \
			--microscope-parameter-name={params.microscopeParameterName} \
			--microscope-chromatic-aberration-name={params.microscopeChromaticAberrationName} \
			--position-name={params.positionName} \
			--data-home={params.dataHome} \
			--analysis-home={params.analysisHome} \
			> {output}
		"""

rule registration:
	input:
		"{analysisPath}/logs/create_analysis.log"

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		registerDrift = config["registration"]["registerDrift"],
		refFrameIndex = config["registration"]["refFrameIndex"],
		highPassFilterSigma = config["registration"]["highPassFilterSigma"],
		registerColor = config["registration"]["registerColor"],
		saveFiducials = config["registration"]["saveFiducials"]

	output:
		file = "{analysisPath}/warpedImages/fov_{fov}_zpos_{zpos}.tif",
		log = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_registration.log"

	shell:
		"""
		python {params.md} register-images \
			--data-set-name={params.dataSetName} \
			--fov={wildcards.fov} \
			--zpos={wildcards.zpos}  \
			--output-name={output.file} \
			--register-drift={params.registerDrift} \
			--ref-frame-index={params.refFrameIndex} \
			--high-pass-filter-sigma={params.highPassFilterSigma} \
			--register-color={params.registerColor} \
			--save-fiducials={params.saveFiducials} \
			--save-fiducials={params.saveFiducials} \
			> {output.log}
		"""
		
rule preprocessing:
	input:
		"{analysisPath}/warpedImages/fov_{fov}_zpos_{zpos}.tif"

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		highPassFilterSigma = config["pre"]["highPassFilterSigma"]
	
	output:
		file = "{analysisPath}/processedImages/fov_{fov}_zpos_{zpos}.npz",
		log = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_processing.log"

	shell:
		"""
		python {params.md} process-images \
			--data-set-name={params.dataSetName} \
			--fov={wildcards.fov} --zpos={wildcards.zpos} \
			--warped-images-name={input} \
			--output-name={output.file} \
			--high-pass-filter-sigma={params.highPassFilterSigma} \
			> {output.log}
		"""

rule decoding:
	input:
		"{analysisPath}/processedImages/fov_{fov}_zpos_{zpos}.npz"
	
	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		borderSize = config["decoding"]["borderSize"],
		magnitudeThreshold = config["decoding"]["magnitudeThreshold"],
		distanceThreshold = config["decoding"]["distanceThreshold"]
	
	output:
		file = "{analysisPath}/decodedImages/fov_{fov}_zpos_{zpos}.npz",
		log = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_decoding.log"
	
	shell:
		"""
		python {params.md} decode-images \
			--data-set-name={params.dataSetName} \
			--fov={wildcards.fov} \
			--zpos={wildcards.zpos} \
			--decoding-images-name={input} \
			--output-name={output.file} \
			--border-size={params.borderSize} \
			--magnitude-threshold={params.magnitudeThreshold} \
			--distance-threshold={params.distanceThreshold} \
			> {output.log}
		"""

rule trainPSM:
	input:
		expand("{analysisPath}/decodedImages/fov_{fov}_zpos_{zpos}.npz", 
			analysisPath = config["global"]["analysisPath"], 
			fov = range(config["global"]["fovs"]), 
			zpos = config["global"]["zposes"])

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		zposNum = config["trainPSM"]["zposNum"]
	
	output:
		file = "{analysisPath}/pixel_score_machine.pkl",
		log = "{analysisPath}/logs/train_PSM.log"
	
	shell:
		"""
		python {params.md} train-psm \
			--data-set-name={params.dataSetName} \
			--decoded-images-dir={wildcards.analysisPath}/decodedImages \
			--output-name={output.file} \
			--zpos-num={params.zposNum} \
			> {output.log}
		"""

rule extractBarcodes:
	input:
		deocdedImage = "{analysisPath}/decodedImages/fov_{fov}_zpos_{zpos}.npz",
		psm = "{analysisPath}/pixel_score_machine.pkl"

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		maxCores = config["extractBarcodes"]["maxCores"],
		barcodesPerCore = config["extractBarcodes"]["barcodesPerCore"]

	output:
		file = "{analysisPath}/extractedBarcodes/fov_{fov}_zpos_{zpos}.h5",
		log = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_extract_barcodes.log"

	shell:
		"""
		python {params.md} extract-barcodes \
			--data-set-name={params.dataSetName} \
			--fov={wildcards.fov} \
			--zpos={wildcards.zpos} \
			--decoded-images-name={input.deocdedImage} \
			--output-name={output.file} \
			--psm-name={input.psm} \
			--barcodes-per-core={params.barcodesPerCore} \
			--max-cores={params.maxCores} \
			> {output.log}
		"""

rule exportBarcodes:
	input:
		expand("{analysisPath}/extractedBarcodes/fov_{fov}_zpos_{zpos}.h5",
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"])

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"]

	output:
		file = "{analysisPath}/exportedBarcodes/barcodes.h5",
		log = "{analysisPath}/logs/exportedBarcodes.log"

	shell:
		"""
		python {params.md} export-barcodes \
			--data-set-name={params.dataSetName} \
			--decoded-barcodes-name {input} \
			--output-name={output.file} \
			> {output.log}
		"""

rule filterBarcodes:
	input:
		"{analysisPath}/exportedBarcodes/barcodes.h5"

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		fovNum = config["filterBarcodes"]["fovNum"],
		keepBlankBarcodes = config["filterBarcodes"]["keepBlankBarcodes"],
		misIdentificationRate = config["filterBarcodes"]["misIdentificationRate"],
		minAreaSize = config["filterBarcodes"]["minAreaSize"]

	output:
		file = "{analysisPath}/filteredBarcodes/barcodes.h5",
		log = "{analysisPath}/logs/filteredBarcodes.log"

	shell:
		"""
		python {params.md} filter-barcodes \
			--data-set-name={params.dataSetName} \
			--exported-barcodes-name={input} \
			--output-name={output.file} \
			--fov-num={params.fovNum} \
			--keep-blank-barcodes={params.keepBlankBarcodes} \
			--mis-identification-rate={params.misIdentificationRate} \
			--min-area-size={params.minAreaSize} \
			> {output.log}
		"""

rule segmentation:
	input:
		warpedImages = "{analysisPath}/warpedImages/fov_{fov}_zpos_{zpos}.tif"

	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		diameter_DAPI = config["segmentation"]["DAPI"]["diameter"],
		modelType_DAPI = config["segmentation"]["DAPI"]["modelType"],
		diameter_polyT = config["segmentation"]["polyT"]["diameter"],
		modelType_polyT = config["segmentation"]["polyT"]["modelType"],
		gpu = config["segmentation"]["polyT"]["gpu"]

	output:
		log_DAPI = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_DAPI_segmentation.log",
		log_polyT = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_polyT_segmentation.log",
		file_DAPI = "{analysisPath}/segmentedImages/fov_{fov}_zpos_{zpos}_DAPI.npz",
		file_polyT = "{analysisPath}/segmentedImages/fov_{fov}_zpos_{zpos}_polyT.npz"

	shell:
		"""
		python {params.md} segmentation \
			--data-set-name={params.dataSetName} \
			--fov={wildcards.fov} \
			--zpos={wildcards.zpos} \
			--warped-images-name={input.warpedImages} \
			--feature-name=DAPI \
			--output-name={output.file_DAPI} \
			--diameter={params.diameter_DAPI} \
			--model-type={params.modelType_DAPI} \
			--gpu={params.gpu}  \
			> {output.log_DAPI}
		python {params.md} segmentation \
			--data-set-name={params.dataSetName} \
			--fov={wildcards.fov} \
			--zpos={wildcards.zpos} \
			--warped-images-name={input.warpedImages} \
			--feature-name=polyT \
			--output-name={output.file_polyT} \
			--diameter={params.diameter_polyT} \
			--model-type={params.modelType_polyT} \
			--gpu={params.gpu}  \
			> {output.log_polyT}
		"""

rule extractFeatures:
	input:
		DAPI = "{analysisPath}/segmentedImages/fov_{fov}_zpos_{zpos}_DAPI.npz",
		polyT = "{analysisPath}/segmentedImages/fov_{fov}_zpos_{zpos}_polyT.npz",
		
	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"]

	output:
		file_DAPI = "{analysisPath}/extractedFeatures/fov_{fov}_zpos_{zpos}_DAPI.shp",
		log_DAPI  = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_DAPI_extract_feature.log",
		file_ployT = "{analysisPath}/extractedFeatures/fov_{fov}_zpos_{zpos}_polyT.shp",
		log_polyT  = "{analysisPath}/logs/fov_{fov}_zpos_{zpos}_polyT.log"

	shell:
		"""
		python {params.md} extract-features \
		        --data-set-name={params.dataSetName} \
		        --fov={wildcards.fov} \
		        --zpos={wildcards.zpos} \
		        --segmented-images-name={input.DAPI} \
		        --output-name={output.file_DAPI} \
		        > {output.log_DAPI}
		
		python {params.md} extract-features \
		        --data-set-name={params.dataSetName} \
		        --fov={wildcards.fov} \
		        --zpos={wildcards.zpos} \
		        --segmented-images-name={input.polyT} \
		        --output-name={output.file_ployT} \
		        > {output.log_polyT}
		"""


rule exportFeatures_DAPI:
	input:
		expand("{analysisPath}/extractedFeatures/fov_{fov}_zpos_{zpos}_DAPI.shp",
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"])
	params:
		dataSetName = config["global"]["dataSetName"],
		bufferSize = config["exportFeatures"]["bufferSize"],
		md = config["global"]["merfishdecoder"]

	output:
		file = "{analysisPath}/exportedFeatures/DAPI.shp",
		log = "{analysisPath}/logs/export_feature_DAPI.log"
	
	shell:
		"""
		python {params.md} export-features \
		        --data-set-name={params.dataSetName} \
		        --segmented-features-name {input} \
		        --output-name={output.file} \
		        --buffer-size={params.bufferSize} \
		        > {output.log}
		"""

rule exportFeatures_ployT:
	input:
		expand("{analysisPath}/extractedFeatures/fov_{fov}_zpos_{zpos}_polyT.shp",
			analysisPath = config["global"]["analysisPath"],
			fov = range(config["global"]["fovs"]),
			zpos = config["global"]["zposes"])

	params:
		dataSetName = config["global"]["dataSetName"],
		bufferSize = config["exportFeatures"]["bufferSize"],
		md = config["global"]["merfishdecoder"]

	output:
		file = "{analysisPath}/exportedFeatures/polyT.shp",
		log = "{analysisPath}/logs/export_feature_polyT.log"
	
	shell:
		"""
		python {params.md} export-features \
		        --data-set-name={params.dataSetName} \
		        --segmented-features-name {input} \
		        --output-name={output.file} \
		        --buffer-size={params.bufferSize} \
		        > {output.log}
		"""

rule filterFeatures:
	input:
		polyT = "{analysisPath}/exportedFeatures/polyT.shp",
		DAPI = "{analysisPath}/exportedFeatures/DAPI.shp"

	params:
		dataSetName = config["global"]["dataSetName"],
		borderSize = config["filterFeatures"]["borderSize"],
		minZplane = config["filterFeatures"]["minZplane"],
		md = config["global"]["merfishdecoder"]

	output:
		file_DAPI  = "{analysisPath}/filteredFeatures/DAPI.shp",
		file_polyT = "{analysisPath}/filteredFeatures/polyT.shp",
		log_DAPI  = "{analysisPath}/logs/filter_features_DAPI.log",
		log_polyT = "{analysisPath}/logs/filter_features_polyT.log"

	shell:
		"""
		python {params.md} filter-features \
			--data-set-name={params.dataSetName} \
			--exported-features-name={input.DAPI} \
			--output-name={output.file_DAPI} \
			--border-size={params.borderSize} \
			--min-zplane={params.minZplane} \
			> {output.log_DAPI}
		python {params.md} filter-features \
			--data-set-name={params.dataSetName} \
			--exported-features-name={input.polyT} \
			--output-name={output.file_polyT} \
			--border-size={params.borderSize} \
			--min-zplane={params.minZplane} \
			> {output.log_polyT}
		"""

rule assignBarcodes_DAPI:
	input:
		features = "{analysisPath}/filteredFeatures/DAPI.shp",
		barcodes = "{analysisPath}/filteredBarcodes/barcodes.h5"
	
	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		maxCores = config["assignBarcodes"]["DAPI"]["maxCores"],
		bufferSize = config["assignBarcodes"]["DAPI"]["bufferSize"]

	output:
		file = "{analysisPath}/assignedBarcodes/barcodes_DAPI.h5",		
		log  = "{analysisPath}/logs/assign_barcodes_DAPI.log"
	
	shell:
		"""
		python {params.md} assign-barcodes \
		        --data-set-name={params.dataSetName} \
		        --exported-barcodes-name={input.barcodes} \
		        --exported-features-name={input.features} \
		        --output-name={output.file} \
		        --max-cores={params.maxCores} \
		        --buffer-size={params.bufferSize}
		        > {output.log}
		"""

rule assignBarcodes_polyT:
	input:
		features = "{analysisPath}/filteredFeatures/polyT.shp",
		barcodes = "{analysisPath}/filteredBarcodes/barcodes.h5"
	
	params:
		dataSetName = config["global"]["dataSetName"],
		md = config["global"]["merfishdecoder"],
		maxCores = config["assignBarcodes"]["DAPI"]["maxCores"],
		bufferSize = config["assignBarcodes"]["DAPI"]["bufferSize"]

	output:
		file = "{analysisPath}/assignedBarcodes/barcodes_polyT.h5",		
		log  = "{analysisPath}/logs/assign_barcodes_polyT.log"
	
	shell:
		"""
		python {params.md} assign-barcodes \
		        --data-set-name={params.dataSetName} \
		        --exported-barcodes-name={input.barcodes} \
		        --exported-features-name={input.features} \
		        --output-name={output.file} \
		        --max-cores={params.maxCores} \
		        --buffer-size={params.bufferSize}
		        > {output.log}
		"""

rule filterBarcodesFeature:
	input:
		polyT = "{analysisPath}/assignedBarcodes/barcodes_polyT.h5",
		DAPI = "{analysisPath}/assignedBarcodes/barcodes_DAPI.h5"
	
	params:
		dataSetName = config["global"]["dataSetName"],
		fovNum = config["filterBarcodes"]["fovNum"],
		md = config["global"]["merfishdecoder"],
		keepBlankBarcodes = config["filterBarcodes"]["keepBlankBarcodes"],
		misIdentificationRate = config["filterBarcodes"]["misIdentificationRate"],
		minAreaSize = config["filterBarcodes"]["minAreaSize"]
	
	output:
		file_polyT = "{analysisPath}/filteredBarcodes/barcodes_polyT.h5",
		file_DAPI  = "{analysisPath}/filteredBarcodes/barcodes_DAPI.h5",
		log_polyT  = "{analysisPath}/logs/filter_barcodes_polyT.log",
		log_DAPI   = "{analysisPath}/logs/filter_barcodes_DAPI.log"
	
	shell:
		"""
		python {params.md} filter-barcodes \
		        --data-set-name={params.dataSetName} \
		        --exported-barcodes-name={input.DAPI} \
		        --output-name={output.file_DAPI} \
		        --fov-num={params.fovNum} \
		        --keep-blank-barcodes={params.keepBlankBarcodes} \
		        --mis-identification-rate={params.misIdentificationRate} \
		        --min-area-size={params.minAreaSize} \
		        > {output.log_DAPI}
		python {params.md} filter-barcodes \
		        --data-set-name={params.dataSetName} \
		        --exported-barcodes-name={input.polyT} \
		        --output-name={output.file_polyT} \
		        --fov-num={params.fovNum} \
		        --keep-blank-barcodes={params.keepBlankBarcodes} \
		        --mis-identification-rate={params.misIdentificationRate} \
		        --min-area-size={params.minAreaSize} \
		        > {output.log_polyT}
		"""

